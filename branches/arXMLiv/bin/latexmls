#!/usr/bin/perl -w

#######################
# A Server for LaTeXML
#######################

eval 'exec /usr/bin/perl -w -S $0 ${1+"$@"}'
    if 0; # not running under some shell
use strict;
use warnings;
use FindBin;
use lib "$FindBin::RealBin/../lib";
use Getopt::Long qw(GetOptionsFromString :config no_ignore_case);
use Pod::Usage;
use LaTeXML;
use LaTeXML::Util::Pathname;
use LaTeXML::Util::ObjectDB;
use LaTeXML::Util::WWW;
use LaTeXML::Post;
use LaTeXML::Post::Writer;
use LaTeXML::Post::Scan;
use Data::Dumper;


use Carp;
use Clone qw(clone);
use POSIX qw( setsid );
use IO::Socket;

#**********************************************************************
# Ports
my %portlist = ( math=>3334, fragment=>3344, standard=>3354);
# Auto-flush / restart process on how many processed sources:
my %flushlist = ( math=> 5000, fragment=>1000, standard=>100, default=>100);

# Global Options
my $g_opts = {
 nopop=>1, identity => "latexmls (LaTeXML version $LaTeXML::VERSION)",
 verbosity=>0,  strict=>0,  comments=>1,  noparse=>0,  includestyles=>0,
 post=>0, parallelmath=>0, input_counter=>0, input_limit=>0,
 local=>0, embed=>0, timeout=>60, format=>'xml', mode=>'standard',
 destination=>q{}, postdest=>q{}, log=>q{}, postlog=>q{}, base=>q{},
 procs_post=>{}, help=>0, showversion=>0, preamble=>q{}, preamble_loaded=>q{},
 stylesheet=>q{},defaultcss=>1,summary=>0,icon=>0, inputencoding=>q{}, 
 documentid =>q{}, type=>'auto', css => [], paths => [q{.}], preload=>[], debugs=>[],
 authlist=>{}, port=>q{}, ARGV_SAVED => [@ARGV] #Save, so that we reinvoke when flushed
};

# Read global options

read_options($g_opts,join(" ",@ARGV));

#**********************************************************************
# Set up the server
$g_opts->{port} = $portlist{$g_opts->{mode}} unless $g_opts->{port};
my $server = setup_server($g_opts->{port});

#**********************************************************************
# Set up the processing.
print STDERR $g_opts->{identity}."\n" if $g_opts->{verbosity} >= 0;
$g_opts->{mode} = lc($g_opts->{mode});

# Configure auto-flush limit, if needed:
if (!$g_opts->{input_limit} || $g_opts->{input_limit}<1) {
  $g_opts->{input_limit}=$flushlist{$g_opts->{mode}}||$flushlist{default};
}

# When in math mode, preload math packages:
if ($g_opts->{mode} eq "math") {
    my  @mathpreload = ("LaTeX.pool", "article.cls", "amsmath.sty", "amsthm.sty", "amstext.sty", "amssymb.sty", "eucal.sty");
    @{$g_opts->{preload}}=(@{$g_opts->{preload}},@mathpreload);
}

# Make paths canonical:
@{$g_opts->{paths}} = map {pathname_canonical($_)} @{$g_opts->{paths}};

# Setup Local Options
my $opts = {};
$opts = clone($g_opts);

# Setup HTTP::Response var for URI requests
my $response;

# Install signal-handlers
local $SIG{'ALRM'} = 'dotimeout';
local $SIG{'TERM'} = 'doterm';
local $SIG{'INT'} = 'doterm';

binmode(STDERR,":utf8");
# Prepare LaTeXML object
my $latexml= new_latexml($g_opts);

print STDERR "\n"; #Sanitize log.
my $digested_preamble = load_preamble($g_opts,$latexml,undef);

# Setting up Daemon
my $init_status = $latexml->getStatusMessage;
print STDERR "\n"; #Sanitize log.
if ($init_status !~ /error/i) {
  daemonize();
} else {  croak $init_status; } # Demand errorless initialization

# Pass on authlist:
$latexml->withState(sub {
  my($state)=@_;
  $state->assignValue('_authlist',$opts->{authlist},'global');
});

# Set timeout, if wanted:
alarm($g_opts->{timeout});              # Timeout if inactive for too long...
print STDERR "\n\nInitialization complete...\n";

#**********************************************************************
# Daemon has been set up, now process files on demand:
while ( my $client = $server->accept() ) {
  my $source = q{};
  my $sockstart=1;
  # Accept customizations for each input:
  # Setup Local Options
  $opts = clone($g_opts);
  while (my $line = <$client>) {
    $sockstart=0 unless $line=~/^--/;# Assume no options will be given if the line does not start with --
    if ($sockstart) {
      # Parse overwrites from current input:
      my ($ret,$args) = read_options($opts,$line);
      if ($opts->{base} && $opts->{base}!~/(\w+):\/\//) 
        { chdir $opts->{base} or croak "Can't chdir to ".$opts->{base}.": $!";};
      if (!$opts->{local} && ($opts->{destination} || $opts->{log} || $opts->{postdest}
          || $opts->{postlog}))
        {carp "I/O from filesystem not allowed without --local!\n".
           " Will revert to sockets!\n";
         undef $opts->{destination}; undef $opts->{log};
         undef $opts->{postdest}; undef $opts->{postlog}; }
      $source.=join(" ", grep {$_ !~ /^--/} @$args);
      $sockstart=0; next;
    }
    last if ($line =~ /^(\s*)END REQUEST(\s*)$/);
    $line .= "\n" unless ((! defined $line) || ($line =~ /\n$/));
    $source .= $line if (defined $line);
  }
  next unless ((defined $source) && (length( $source ) > 0));
  # Bookkeeping...
  alarm(0);                    # We are processing, ignore timeout here
  $opts->{input_counter}++;
  # Determine input type if local, otherwise only socket exchange:
  my ($isURL,$isString,$isFile);
  if ($opts->{local}) {
    $source=~s/\n$//g; # Eliminate trailing new lines
    my @source_lines = split(/\n/,$source);
    $isString = $source if (scalar(@source_lines)>1 || $opts->{mode} eq "math");
    unless ($isString) {
      $isFile = pathname_find($source,types=>['tex',q{}]);
      $isFile = pathname_canonical($isFile);
      $source = $isFile if $isFile;
      unless($isFile) {
        $response = auth_get($source,$opts->{authlist});
        $isURL = $response->is_success;
      }
    }
    $isString = $source unless ($isFile || $isURL); # Not file or URL - then process as string
  } else { $isString=$source; }
  if (!$opts->{local} && ($isFile)) {carp "I/O from filesystem not allowed without --local!\n".
                                                " Will revert to sockets!\n";}
  # Any post switch implies post:
  $opts->{post}=1 if (keys %{$opts->{procs_post}});
  #Instantiate targets for current input
  my ($dir,$name,$ext);
  ($dir,$name,$ext) = pathname_split($source) if $isFile;
  #Redirect to logfile, if requested:
  my ($clog);
  if ($opts->{log} && $opts->{local}) {
    $clog = $opts->{log};
    $clog=~s/#name/$name/eg;
    $clog=~s/#dir/$dir/eg;
    $clog=~s/#ext/$ext/eg;
    open(LOG,">",$clog) or croak "Couldn't open log file $clog : $!\n";
    *ERRORIG=*STDERR;
    *STDERR = *LOG;
  } else {
    #No log if not local (STDERR redirects to /dev/null)
  }
  my $cdest;
  if ($opts->{destination} && $opts->{local}) {
    $cdest = $opts->{destination};
    $cdest =~ s/#name/$name/eg;
    $cdest =~ s/#dir/$dir/eg;
    $cdest =~ s/#ext/$ext/eg;
  } else { #Output to client
    $cdest = $client;
  }
  my $cdestpost;
  if ($opts->{postdest} && $opts->{local}) {
    $cdestpost = $opts->{postdest};
    $cdestpost =~ s/#name/$name/eg;
    $cdestpost =~ s/#dir/$dir/eg;
    $cdestpost =~ s/#ext/$ext/eg;
  } else { #Output to $cdest!
    $cdestpost = $cdest;
    $cdestpost.=".xhtml" unless
      (!$cdestpost || ($cdestpost eq $client) || $cdestpost=~/\.xhtml$/ || 
       $cdestpost=~s/\.tex\.xml$/.xhtml/ || $cdestpost=~s/\.xml$/.xhtml/); #Some heuristics
  }
  $cdestpost = undef unless $opts->{post};

  my $deststat = (stat($cdest))[9] if ($opts->{summary} && $cdest && ($cdest ne $client));
  my $destpoststat = (stat($cdestpost))[9] if ($opts->{summary} && $cdestpost && ($cdestpost ne $client));

  ###########################################################################
  #   Start of conversion
  #

  $opts->{type} = 'bibtex' if ($opts->{type} eq 'auto') && ($source =~ /\.bib$/);
  # ========================================
  # First read and digest whatever we're given.
  my $digested;
  if ($opts->{mode} eq "standard") {
    # In the classic case, we need to reinitialize latexml on every input
    undef $latexml;
    $latexml= new_latexml($opts);
    # Preload the preamble if any (always - standard mode here)
    undef $opts->{preamble_loaded};
    $digested_preamble = load_preamble($opts,$latexml,$digested_preamble);
    print STDERR "\n";          # Again, flush log
  } # End of standard use case

  # Preload the preamble if any (and not loaded)
  $digested_preamble = load_preamble($opts,$latexml,$digested_preamble);
  # Prepare daemon frame
  $latexml->withState(sub {
                        my($state)=@_; # Sandbox state
                        # Save preamble information for further use:
                        $state->assignValue('_preamble_loaded',$opts->{preamble},'global');
                        $state->assignValue('_authlist',$opts->{authlist},'global');
                        $state->pushDaemonFrame; });
  # Prepare preamble and postamble in fragment mode
  my ($serialized,$dom,$modepre,$modepost);
  if ($opts->{mode} eq 'fragment') {
    $modepre='standard_preamble.tex';
    $modepost='standard_postamble.tex';
  }
  # Digest source:
  eval {
    if ($isURL) {
      $digested = $latexml->digestString($response->content,preamble=>$modepre,postamble=>$modepost,source=>$source,noinitialize=>1);
    } elsif ($isFile) {
      if ($opts->{type} eq 'bibtex') {
        # TODO: Do we want URL support here?
        $digested = $latexml->digestBibTeXFile($source,preamble=>$modepre,postamble=>$modepost,noinitialize=>1);
      } else {
        $digested = $latexml->digestFile($source,preamble=>$modepre,postamble=>$modepost,noinitialize=>1);
      }}
    elsif ($isString) {
      $source = mathdoc($source) if ($opts->{mode} eq "math");
      $digested = $latexml->digestString($source,preamble=>$modepre,noinitialize=>1);
    }
    # ========================================
    # Now, convert to DOM and output, if desired.
    if ($digested) {
      $digested = LaTeXML::List->new($digested_preamble,$digested) if defined $digested_preamble;
      local $LaTeXML::Global::STATE = $$latexml{state};
      if ($opts->{format} eq 'tex') {
        $serialized = LaTeXML::Global::UnTeX($digested);
      } elsif ($opts->{format} eq 'box') {
        $serialized = $digested->toString;
      } else {
        $dom = $latexml->convertDocument($digested);
        $serialized = $dom->toString(1);
      }
    }
    1;
  } or do {# Fatal occured!
    print STDERR "$@\n";
    print STDERR "\nConversion complete: ".$latexml->getStatusMessage.".\n";
    if ($opts->{summary}) {
      my $summary_setup = {
       cdest=>$cdest,
       cdestpost=>$cdestpost,
       client=>$client,
       deststat=>$deststat,
       destpoststat=>$destpoststat,
       message=>$latexml->getStatusMessage };
      print_summary($summary_setup);
    }
    # Need to reset latexml in these cases
    if ($opts->{log}) {# Close log file
      close(LOG); *STDERR = *ERRORIG;
    }
    # Undef variables
    map {undef $_} ($latexml, $digested, $dom, $serialized, $source);
    undef $opts->{preamble_loaded};
    # Re-Init:
    $latexml = new_latexml($opts);
    # Preload the preamble if any
    $digested_preamble = load_preamble($opts,$latexml,$digested_preamble);
    print STDERR "\n";          # Again, flush log
    # Move to next...
    flush_daemon($opts);
    alarm($opts->{timeout});              # Timeout if inactive for too long...
    next;                       # Move on, no body to be produced.
  };
  print STDERR "\nConversion complete: ".$latexml->getStatusMessage.".\n";
  my $summaryMessage = $latexml->getStatusMessage;
  # End daemon run, by popping frame:
  $latexml->withState(sub {
                        my($state)=@_; # Remove current state frame
                        print STDERR Dumper($$state{undo}[0]),"\n";
                        $state->popDaemonFrame unless $opts->{nopop};
                        $$state{status} = {};
                      });
  if ($cdest && $opts->{local} && ($cdest ne $client)) {
    open(OUT,">",$cdest) or croak "Couldn't open output file $cdest: $!";
  } elsif ($cdest==$client) {
    *OUT = *$cdest;
  }
  else {
    *OUT = *STDOUT;
  }
  # Fall back to default post processors if no preferences:
  %{$opts->{procs_post}}=%{$g_opts->{procs_post}} unless (scalar(keys %{$opts->{procs_post}}));
  # Fall back to pmml as default
  $opts->{procs_post}->{'pmml'}=1 unless (scalar(keys %{$opts->{procs_post}}));

  print OUT $serialized if ($serialized && (!$opts->{post} || ($cdest ne $client)));
  if ($cdest && $opts->{local} && ($cdest ne $client)) {
    close(OUT);
  }
  if ($opts->{log} && $opts->{local}) { # Close log file
    close(LOG); *STDERR = *ERRORIG;
  }

  if ($opts->{post} && $dom && (!$opts->{noparse})) {
    # Postprocess
    # Prepare post log and destination
    my $clogpost;
    $opts->{postlog} = $opts->{log} unless $opts->{postlog};
    if ($opts->{postlog} && $opts->{local}) {
      $clogpost = $opts->{postlog};
      $clogpost=~s/#name/$name/eg;
      $clogpost=~s/#dir/$dir/eg;
      $clogpost=~s/#ext/$ext/eg;
      my $wmode = ">";
      $wmode = ">>" if (($clogpost eq $clog) && (-e $clog));
      open(PLOG,"$wmode",$clogpost) or croak "Couldn't open log file $clog : $!\n";
      *ERRORIG=*STDERR;
      *STDERR = *PLOG;
      if ($clog eq $clogpost) {
        print STDERR "---------\n";
        print STDERR "LaTeXML Post-processing: \n";
      }
    } else {
      # No log if not local (STDERR redirects to /dev/null)
    }
    # Perform post-processing conversion...
    my $post_setup = {
     dest=>$cdestpost,stylesheet=>$opts->{stylesheet},
     dom=>$dom,mode=>$opts->{mode},
     format=>$opts->{format}, parallelmath=>$opts->{parallelmath},
     procs_post=>$opts->{procs_post},isStream=>($cdestpost eq $client),
     verbosity=>$opts->{verbosity},defaultcss=>$opts->{defaultcss},embed=>$opts->{embed}
    };
    execute_post($post_setup);
    if ($opts->{postlog} && $opts->{local}) { #Close log file
      close(PLOG); *STDERR = *ERRORIG;
    }
  }
  # Print summary, if requested
  if ($opts->{summary}) {
    my $summary_setup = {
      cdest => $cdest,
      cdestpost=>$cdestpost,
      client=>$client,
      deststat=>$deststat,
      destpoststat=>$destpoststat,
      message=>$summaryMessage };
    print_summary($summary_setup);
  }
  close $client;
  # Undef variables
  undef $latexml if $opts->{mode} eq "standard";
  map {undef $_} ( $digested, $dom, $serialized, $source, $clog);
  # Move to next...
  flush_daemon($opts);
  alarm($opts->{timeout});              #Timeout if inactive for too long...
}

#======================================================================
# Helpers
#======================================================================
sub execute_post {
  my ($opts)=@_;
  my ($postdest,$style,$dom,$extract,$parallel,$proctypes,$isStream,$format,$verbosity,$defaultcss,$embed)= 
    map {$opts->{$_}} qw(dest stylesheet dom mode parallelmath procs_post isStream format verbosity defaultcss embed);
  $verbosity = $verbosity||0;
  my %PostOPS = (verbosity=>$verbosity,siteDirectory=>".");
  #Postprocess
  #Default is XHTML, XML otherwise (TODO: Expand)
  $format="xml" if ($style);
  $format="xhtml" unless ($format eq "xml");
  $style = "LaTeXML-xhtml.xsl" unless $style;
  my @css=();
  unshift (@css,"core.css") if ($defaultcss && !($extract eq "math"));
  $parallel = $parallel||0;
  my $doc;
  eval {
    $doc = LaTeXML::Post::Document->newFromString($dom->toString(1),nocache=>1,destination=>$postdest); 
    1;}
    or do {                     #Fatal occured!
      #Since this is postprocessing, we don't need to do anything
      #   just avoid crashing... and exit
      undef $doc;
      print STDERR "FATAL: Post-processor crashed! $@\n";
      return;
    };
  require LaTeXML::Post::MathML;
  require LaTeXML::Post::OpenMath;
  require LaTeXML::Post::PurgeXMath;
  my @mprocs=();

  push (@mprocs, LaTeXML::Post::MathML::Presentation->new(%PostOPS)) if $$proctypes{'pmml'};
  push (@mprocs, LaTeXML::Post::MathML::Content->new(%PostOPS)) if $$proctypes{'cmml'};
  push (@mprocs, LaTeXML::Post::OpenMath->new(%PostOPS)) if $$proctypes{'openmath'};
  my $main = shift(@mprocs);
  $main->setParallel(@mprocs) if $parallel;
  $main->keepTeX if ($$proctypes{'keepTeX'} && $parallel);
  my @procs=();
  push(@procs,$main);
  push(@procs,@mprocs) unless $parallel;
  push(@procs, LaTeXML::Post::PurgeXMath->new(%PostOPS)) unless $$proctypes{'keepXMath'};
  require LaTeXML::Post::XSLT;
  my @csspaths=();
  if (@css) {
    foreach my $css (@css) {
      $css .= '.css' unless $css =~ /\.css$/;
      # Dance, if dest is current dir, we'll find the old css before the new one!
      my @csssources = map {pathname_canonical($_)}
                           pathname_findall($css,types=>['css'],
                                            (),
                                            installation_subdir=>'style');
      my $csspath = pathname_absolute($css,pathname_directory('.'));
      while (@csssources && ($csssources[0] eq $csspath)) {
        shift(@csssources);
      }
      my $csssource = shift(@csssources);
      pathname_copy($csssource,$csspath)  if $csssource && -f $csssource;
      push(@csspaths,$csspath);
    }}
  push(@procs,LaTeXML::Post::XSLT->new(stylesheet=>$style,
					 parameters=>{number_sections
						      =>("true()"),
                                                      (@csspaths ? (CSS=>[@csspaths]):()),},
                                       %PostOPS)) if $style;
  push(@procs,LaTeXML::Post::Writer->new(format=>$format,omit_doctype=>0,%PostOPS))
    if ($postdest && !$isStream);
  my $postdoc;
  eval { ($postdoc) = LaTeXML::Post::ProcessChain($doc,@procs); 1;}
  or do {                     #Fatal occured!
    #Since this is postprocessing, we don't need to do anything
    #   just avoid crashing... and exit
    undef $doc;
    undef $postdoc;
    undef $_ foreach (@procs);
    undef $_ foreach (@mprocs);
    undef @procs;
    undef @mprocs;
    print STDERR "FATAL: Post-processor crashed! $@\n";
    return;
  };
  # If we want an embedable snippet, unwrap to body's "main" div
  if ($embed && ($extract eq "fragment")) {
    my $bodyel = $postdoc->findnode('//*[local-name()="body"]');
    my $topdiv;
    # Doing monkey hoops is very annoying, why won't LibXML
    # just fix their XPath support already?!
    foreach ($bodyel->childNodes) {
      $topdiv=$_;
      last if ($topdiv->nodeName eq 'div');
    }
    my $contentdiv;
    foreach ($topdiv->childNodes) {
      $contentdiv=$_;
      last if ($contentdiv->nodeName eq 'div');
    }
    my $maindiv;
    foreach ($contentdiv->childNodes) {
      $maindiv=$_;
      last if ($maindiv->nodeName eq 'div');
    }
    # Assign the top division to the post-document holder
    # Copy over all <html> attributes to the maindiv, so that we keep namespaces:
    my $htmlel = $postdoc->findnode('//*[local-name()="html"]');
    $htmlel->unbindNode;
    $htmlel->removeChildNodes;
    $htmlel->setNodeName('div');
    $htmlel->setAttribute('class','document');
    $maindiv->unbindNode;
    foreach ($maindiv->childNodes) {
      $_->unbindNode;
      $htmlel->addChild($_);
    }
    $postdoc = $htmlel;
  }
  if ($extract eq "math") {
    #Output to destination:
    my $mnodes = $postdoc->findnodes('//m:math');
    if ($mnodes->size <= 1) {
      outputXML($postdoc->findnode('//m:math'),$postdest||q{-},$isStream);
    } else {
      my $ancestor = $postdoc->findnode('//m:math')->parentNode;
      $ancestor = $ancestor->parentNode while ($ancestor->findnodes('.//m:math')->size != $mnodes->size);
      outputXML($ancestor,$postdest||q{-},$isStream);
    }
  }
  elsif ($isStream) {
    #Client requested entire document returned via socket, hence send back serialized:
    my $serialized = $postdoc->toString(1);
    print $postdest $serialized;
  }
  undef $doc;
  undef $postdoc;
  foreach (@procs) { undef $_; }
  foreach (@mprocs) { undef $_; }
  undef @procs;
  undef @mprocs;
  return;
}

sub outputXML {
  my($xml,$destxml,$isStream)=@_;
  # Need to move the node to a new document to set up namespaces
  my $newdoc = XML::LibXML::Document->new("1.0","UTF-8");
  return unless ref $xml;
  $newdoc->setDocumentElement($xml->cloneNode(1));
  my $serialized = $newdoc->toString(1);
  if ($destxml eq q{-}) {
      print $serialized;
  }
  elsif ($isStream) {
    print $destxml $serialized;
  }
  else {
      open(OUT,">",$destxml) or croak "Couldn't open output file $destxml: $!";
      print OUT $serialized;
      close OUT;
  }
  undef $serialized; 
  undef $newdoc;
  return;
}

sub mathdoc {
#======================================================================
# TeX Source
#======================================================================
# First read and digest whatever we're given.
    my ($tex) = @_;
# We need to determine whether the TeX we're given needs to be wrapped in \[...\]
# Does it have $'s around it? Does it have a display math environment?
# The most elegant way would be to notice as soon as we start adding to the doc
# and switch to math mode if necessary, but that's tricky.
# Let's just try a manual hack, looking for known switches...
our $MATHENVS = 'math|displaymath|equation*?|eqnarray*?'
  .'|multline*?|align*?|falign*?|alignat*?|xalignat*?|xxalignat*?|gather*?';
$tex =~ s/\A\s+//m; #as usual, strip leading ...
$tex =~ s/\s+\z//m; # ... and trailing space
if(($tex =~ /\A\$/m) && ($tex =~ /\$\z/m)){} # Wrapped in $'s
elsif(($tex =~ /\A\\\(/m) && ($tex =~ /\\\)\z/m)){} # Wrapped in \(...\)
elsif(($tex =~ /\A\\\[/m) && ($tex =~ /\\\]\z/m)){} # Wrapped in \[...\]
elsif(($tex =~ /\A\\begin\{($MATHENVS)\}/m) && ($tex =~ /\\end\{$1\}\z/m)){}
else {
  $tex = '\[ '.$tex.' \]'; }

my $texdoc = <<"EODOC";
\\begin{document}
\\newcounter{equation}
\\newcounter{Unequation}
$tex
\\end{document}
EODOC
return $texdoc;
}

sub dotimeout {
  print "Daemon timed out after inactivity.\n";
  print "Self destructing...\n";
  kill 'TERM', $$;
  return;
}

sub doterm {
  print STDERR "Exiting...\n";
  exit;
}

sub daemonize {
  #chdir '/'                 or croak "Can't chdir to /: $!";
  #This causes more problems than it solves (e.g. sTeX setup)
  open STDIN, '<', '/dev/null'   or croak "Can't read /dev/null: $!";
  open STDOUT, '>', '/dev/null' or croak "Can't write to /dev/null: $!";
  defined(my $pid = fork)   or croak "Can't fork: $!";
  exit if $pid;
  setsid                    or croak "Can't start a new session: $!";
  *STDERR = *STDOUT         or croak "Can't dup stdout: $!";
  return;
}

sub setup_server {
  my $localport = shift;
  my $sock_server = IO::Socket::INET->new(
        'Proto'     => 'tcp',
        'LocalPort' => $localport,
        'Listen'    => SOMAXCONN,
        'Reuse'     => 1,
    );
  croak "can't setup server" unless $sock_server;
  return $sock_server;
}

sub print_summary {
  my ($opts) = @_;
  my ($cdest,$cdestpost,$client,$deststat,$destpoststat,$summaryMessage) =
    map {$opts->{$_}} qw(cdest cdestpost client deststat destpoststat message);
  my $writesuccess = 0; 
  $writesuccess = 1 if ($cdest && ($cdest ne $client) && ($deststat != (stat($cdest))[9]));
  my $writesuccesspost = 0;
  $writesuccesspost = 1 if ($cdestpost && ($cdestpost ne $client) && ($destpoststat != (stat($cdestpost))[9]));
  print $client $summaryMessage."\n";
  if ($writesuccess && $cdest) {
    print $client "Wrote $cdest\n";
  } elsif ($cdest && ($cdest ne $client)) {
    print $client "Error! Did not write file $cdest\n";
    }
  if (($writesuccesspost) && $cdestpost && ($cdestpost ne $cdest)) {
    print $client "Wrote $cdestpost\n";
  } elsif ($cdestpost && ($cdestpost ne $client) && ($cdestpost ne $cdest)) {
    print $client "Error! Did not write file $cdestpost\n";
  }
}

sub new_latexml {
  my $opts = shift;
  my $latexml = LaTeXML->new(preload=>[@{$opts->{preload}}], searchpaths=>[@{$opts->{paths}}],
                          graphicspaths=>['.'],
			  verbosity=>$opts->{verbosity}, strict=>$opts->{strict},
			  includeComments=>$opts->{comments},inputencoding=>$opts->{inputencoding},
			  includeStyles=>$opts->{includestyles},
			  documentid=>$opts->{documentid},
			  nomathparse=>$opts->{noparse});
  if(my @baddirs = grep {! -d $_} @{$opts->{paths}}){
    warn $opts->{identity}.": these path directories do not exist: ".join(', ',@baddirs)."\n"; }

  $latexml->withState(sub {
                        my($state)=@_;
                        $latexml->initializeState('TeX.pool', @{$$latexml{preload} || []});
                      });
  return $latexml;
}

sub load_preamble {
  my ($opts,$latexml,$original) = @_;
  my $digested = undef;
  # Preload the preamble if any (and not loaded)
  if ($opts->{preamble} && ($opts->{preamble_loaded} ne $opts->{preamble})) {
    #TODO: It is difficult to digest 2 files via LaTeXML into 1 XML output right now...
    my $response=auth_get($opts->{preamble},$opts->{authlist});
    if ($response->is_success) {
      my $content = $response->content;
      $digested = $latexml->digestString($content,source=>$opts->{preamble},noinitialize=>1);
    } else {
      if (!$opts->{local}) { carp "File preamble allowed only when 'local' is enabled!"; return; }
      $digested = $latexml->digestFile($opts->{preamble},noinitialize=>1);
    }
    $opts->{preamble_loaded} = $opts->{preamble};
  }
  $digested = $original unless defined $digested;
  return $digested;
}

sub flush_daemon{
  my $opts = shift;
#Flush daemon, every $input_limit files:
if ($opts->{input_counter}>=$opts->{input_limit}) {
  exec($0,@{$opts->{ARGV_SAVED}})
    or croak ("Daemon autoflush Failed!\n");
}}


sub read_options {
  my ($opts,$line) = @_;
  my ($ret,$args) = GetOptionsFromString($line,
	   "output=s"  => sub {$opts->{destination} = $_[1];},
           "destination=s" => sub {$opts->{destination} = $_[1];},
           "postdest=s" => sub {$opts->{postdest} = $_[1];},
	   "preload=s" => sub { push @{$opts->{preload}}, $_[1]},
	   "preamble=s" => sub {$opts->{preamble} = $_[1];},
           "base=s"  => sub {$opts->{base} = $_[1];},
	   "path=s"    => sub { push @{$opts->{paths}}, $_[1]},
	   "quiet"     => sub { $opts->{verbosity}--; },
	   "verbose"   => sub { $opts->{verbosity}++; },
	   "strict"    => sub { $opts->{strict} = 1; },
	   "xml"       => sub { $opts->{format} = 'xml'; },
	   "tex"       => sub { $opts->{format} = 'tex'; },
	   "box"       => sub { $opts->{format} = 'box'; },
	   "bibtex"    => sub { $opts->{type}='bibtex'; },
	   "noparse"   => sub { $opts->{noparse} = 1; },
	   "mode=s"  => sub { $opts->{mode} = $_[1]; },
           "embed"   => sub { $opts->{embed} = 1; },
	   "autoflush=s" => sub { $opts->{input_limit} = $_[1]; },
           "timeout=s"   => sub { $opts->{timeout} = $_[1]; },
           "port=s"      => sub { $opts->{port} = $_[1]; },
           "local"       => sub { $opts->{local} = 1; },
           "nolocal"       => sub { $opts->{local} = 0; },
	   "log=s"       => sub { $opts->{log} = $_[1]; },
           "postlog=s"   => sub { $opts->{postlog} = $_[1]; },
           "summary"    => sub { $opts->{summary} = 1; },
           "nosummary"    => sub { $opts->{summary} = 0; },
	   "includestyles"=> sub { $opts->{includestyles} = 1; },
	   "inputencoding=s"=> sub { $opts->{inputencoding} = $_[1]; },
	   "post"      => sub {$opts->{post} = 1; },
	   "pmml"      => sub {$opts->{procs_post}->{'pmml'}=1;},
	   "cmml"      => sub {$opts->{procs_post}->{'cmml'}=1;},
	   "openmath"  => sub {$opts->{procs_post}->{'openmath'}=1;},
	   "keepTeX"   => sub {$opts->{procs_post}->{'keepTeX'}=1;},
	   "keepXMath" => sub {$opts->{procs_post}->{'keepXMath'}=1;},
	   "parallelmath" => sub {$opts->{parallelmath} = 1;},
	   "stylesheet=s"=>  sub {$opts->{stylesheet} = $_[1];},
	   "css=s"       =>  sub {$opts->{css}=$_[1];},
	   "defaultcss" =>  sub {$opts->{defaultcss} = 1;},
	   "nodefaultcss" =>  sub {$opts->{defaultcss} = 0;},
	   "comments" =>  sub { $opts->{comments} = 1; },
	   "nocomments"=> sub { $opts->{comments} = 0; },
	   "VERSION"   => sub { $opts->{showversion}=1;},
	   "debug=s"   => sub { eval "\$LaTeXML::$_[1]::DEBUG=1; "; },
           "documentid=s" => sub { $opts->{documentid} = $_[1];},
	   "help"      => sub { $opts->{help} = 1; } ,
	  ) or pod2usage(-message => $opts->{identity}, -exitval=>1, -verbose=>0, -output=>\*STDERR);
pod2usage(-message=>$opts->{identity}, -exitval=>1, -verbose=>2, -output=>\*STDOUT) if $opts->{help};
if (!$opts->{local} && ($opts->{destination} || $opts->{log} || $opts->{postdest} || $opts->{postlog})) 
  {carp "I/O from filesystem not allowed without --local!\n".
     " Will revert to sockets!\n";
   undef $opts->{destination}; undef $opts->{log};
   undef $opts->{postdest}; undef $opts->{postlog};}
# HOWEVER, any post switch implies post:
$opts->{post}=1 if (keys %{$opts->{procs_post}});
if($opts->{showversion}){ print STDERR $opts->{identity}."\n"; exit(1); }
return ($ret,$args);
}

#**********************************************************************
__END__

=head1 NAME

C<latexmls> - A server for latexml, latexmlpost and latexmlmath.
Remains initialized in memory until an inactivity timeout occurs.

=head1 SYNOPSIS

latexmls [options]

 Options:
 --destination=file specifies destination file, requires --local.
 --output=file      [obsolete synonym for --destination]
 --postdest=file    specifies destination file for postprocessing,
                    requires --local, --post
 --preload=module   requests loading of an optional module;
                    can be repeated
 --includestyles    allows latexml to load raw *.sty file;
                    by default it avoids this.
 --preamble=file    loads a tex file containing document frontmatter.
                    Useful for fragment mode.
 --base=dir         Specifies the base directory that the server
                    operates in. Useful when converting documents 
                    that employ relative paths.
 --path=dir         adds dir to the paths searched for files,
                    modules, etc; 
 --log=file         specifies log file, reuqires --local (default: STDERR)
 --postlog=file     specifies log file for postprocessing, 
                    requires --local, --post 
                    default is appending to the --log file.
 --summary          print a one line summary message of 
                    the conversion outcome
 --autoflush=count  Automatically restart the daemon after 
                    "count" inputs. Good practice for vast batch 
                    jobs. (default: 10000)
 --timeout=secs     Set a timeout value for inactivity.
                    Default is 60 seconds, set 0 to disable.
 --port=number      Specify server port (default: 3334 for math, 
                    3344 for fragment and 3354 for standard)
 --local            Request a local server (default: off)
                    Required for the --log and --destination switches
                    Required for processing filenames on input
 --documentid=id    assign an id to the document root.
 --quiet            suppress messages (can repeat)
 --verbose          more informative output (can repeat)
 --strict           makes latexml less forgiving of errors
 --bibtex           processes the file as a BibTeX bibliography.
 --xml              requests xml output (default).
 --tex              requests TeX output after expansion.
 --box              requests box output after expansion
                    and digestion.
 --noparse          suppresses parsing math (default: off)
 --mode=modename    Specify daemon parsing mode
                    Supported: standard|math|fragment
                    (default: standard)
 --post             requests a followup post-processing
 --embed            requests an embeddable XHTML div (= document body)
                    (requires: --post,--mode=fragment, socket destination)
 --stylesheet       specifies a stylesheet,
                    to be used by the post-processor.
 --css=cssfile           adds a css stylesheet to html/xhtml
                         (can be repeated)
 --nodefaultcss          disables the default css stylesheet
 --pmml             converts math to Presentation MathML
                    (default for xhtml format)
 --cmml             converts math to Content MathML
 --openmath         converts math to OpenMath 
 --parallelmath     requests parallel math markup for MathML
                    (off by default)
 --keepTeX          keeps the TeX source of a formula as a MathML
                    annotation element (requires --parallelmath)
 --keepXMath        keeps the XMath of a formula as a MathML
                    annotation-xml element (requires --parallelmath)
 --nocomments       omit comments from the output
 --inputencoding=enc specify the input encoding.
 --VERSION          show version number.
 --debug=package    enables debugging output for the named
                    package
 --help             shows this help message.

In I<math> C<mode>, latexmls accepts one TeX formula on input.
In I<standard> and I<fragment> C<mode>, latexmls accepts one I<texfile>
filename per line on input, but only when --local is specified.
If I<texfile> has an explicit extension of C<.bib>, it is processed
as a BibTeX bibliography.

Each communication session allows local option overwriting on the first sent line.
The input will be read until a line containing "END OF REQUEST" is found. Please
take this into account to avoid hanging.

=head1 ToDo List

This is an early release, which has a list of future features planned. They include:
- Support fragments and standard LaTeX files sent via the socket, in addition to the filename scheme.
- Allow log transmission.

=head1 OPTIONS AND ARGUMENTS

=over 4

=item C<--destination>=I<file>

Requires: C<--local>

Specifies the destination file; by default the XML is written to stdout.
The Daemon allows using the #name, #dir and #ext patterns as variables for
the base of the source filename, the source directory and the source file extension.
An example would be: C<--destination=#dir/#name.xml>

=item C<--postdest>=I<file>

Requires: C<--local>, C<--post>

Behaves like C<--destination>, for the post-processing target.
makes use of the C<#dir>,C<#name>,C<#ext> patterns. 

If omitted and C<--destination> is not provided, but C<--post> is specified, 
the output will be returned via the socket. If C<--destination> is present, 
but C<--postdest> is omitted, the default will substitute the extension 
from C<.tex.xml> or C<.xml> to C<.xhtml>. If the C<--destination> extension is different, 
C<.xhtml> will be simply appended.

Note that for output formats different from .xhtml one should always specify
the C<--postdest> option.

=item C<--preload>=I<module>

Requests the loading of an optional module or package.  This may be useful if the TeX code
does not specificly require the module (eg. through input or usepackage).
For example, use C<--preload=LaTeX.pool> to force LaTeX mode.

=item C<--includestyles>

This optional allows processing of style files (files with extensions C<sty>,
C<cls>, C<clo>, C<cnf>).  By default, these files are ignored  unless a latexml
implementation of them is found (with an extension of C<ltxml>).

These style files generally fall into two classes:  Those
that merely affect document style are ignorable in the XML.
Others define new markup and document structure, often using
deeper LaTeX macros to achieve their ends.  Although the omission
will lead to other errors (missing macro definitions), it is
unlikely that processing the TeX code in the style file will
lead to a correct document.

=item C<--path>=I<dir>

Add I<dir> to the search paths used when searching for files, modules, style files, etc;
somewhat like TEXINPUTS.  This option can be repeated.

=item C<--log>=I<file>

Requires: C<--local>

Specifies the log file; be default any conversion messages are printed to stderr.
The Daemon allows using the C<#name>, C<#dir> and C<#ext> patterns as variables for
the base of the source filename, the source directory and the source file extension.
An example would be: C<--log=#dir/#name.log>

=item C<--postlog>=I<file>

Requires: C<--local>, C<--post>

Behaves like C<--log>, but reports on the status of the post-processing exclusively.
The C<#name>,C<#dir> and C<#ext> patterns can be used.
Default is merging this log at the original C<--log> target

=item C<--summary>

Return a one line summary message of the conversion outcome. The message is always sent back
via the socket, strictly after any other return values possible.

=item C<--autoflush>=I<count>

Automatically restart the daemon after converting "count" inputs.
Good practice for vast batch jobs. (default: 10000)

=item C<--timeout>=I<secs>

Set an inactivity timeout value in seconds. If the daemon is not given any input
for the timeout period it will automatically self-destruct.
The default value is 60 seconds, set to 0 to disable.

=item C<--port>=I<number>

Specify server port (default: 3334 for math, 3344 for fragment and 3354 for standard)

=item C<--local>

Request a local server (default: off)
Required for the C<--log> and C<--destination> switches
Required for processing filenames on input, as well as for any filesystem access.
If switched off, the only means of communication with the server is via the respective socket.
Caveat: When C<--local> is disabled, fatal errors cause an empty output at the moment.

=item C<--documentid>=I<id>

Assigns an ID to the root element of the XML document.  This ID is generally
inherited as the prefix of ID's on all other elements within the document.
This is useful when constructing a site of multiple documents so that
all nodes have unique IDs.

=item C<--quiet>

Reduces the verbosity of output during processing, used twice is pretty silent.

=item C<--verbose>

Increases the verbosity of output during processing, used twice is pretty chatty.
Can be useful for getting more details when errors occur.

=item C<--strict>

Specifies a strict processing mode. By default, undefined control sequences and
invalid document constructs (that violate the DTD) give warning messages, but attempt
to continue processing.  Using C<--strict> makes them generate fatal errors.

=item C<--bibtex>

Forces latexml to treat the file as a BibTeX bibliography.
Note that the timing is slightly different than the usual
case with BibTeX and LaTeX.  In the latter case, BibTeX simply
selects and formats a subset of the bibliographic entries; the
actual TeX expansion is carried out when the result is included
in a LaTeX document.  In contrast, latexml processes and expands
the entire bibliography; the selection of entries is done
during post-processing.  This also means that any packages
that define macros used in the bibliography must be
specified using the C<--preload> option.

=item C<--xml>

Requests XML output; this is the default.

=item C<--tex>

Requests TeX output for debugging purposes;  processing is only carried out through expansion and digestion.
This may not be quite valid TeX, since Unicode may be introduced.

=item C<--box>

Requests Box output for debugging purposes;  processing is carried out through expansion and digestions,
and the result is printed.

=item C<--mode>

fragment: Parses frontmatter and \begin{document}...\end{document} fragments.
          Assumes that the documentclass is fixed (if any), which achieves a big speedup,
          since no state reinitialization is necessary. Use the C<--preload> switch
          to load the document class when invoking the daemon.
          Example: C<latexmls --preload=LaTeX.pool --preload=article.cls --mode=fragment>

standard: Default, turns on the classic, unoptimized processing of documents as performed by latexml.
          This is suitable for processing sets of heterogeneous documents, but is slower in general.
          Example: C<latexmls --mode=standard>

math:     Like latexmlmath, parses a single TeX formula per line.
          Example: C<latexmls --mode=math --pmml --keepTeX --parallelmath>

=item C<--post>

Request post-processing of converted XML to XHTML. Default behaviour is
creating XHTML output with Presentational MathML.

=item C<--embed>

Requests an embeddable XHTML div (requires: --post, --mode=fragment), respectively the
top division of the document's body.
Caveat: This experimental mode is enabled only for fragment mode and post-processed
documents (to XHTML). Also, this option can not be used if an explicit --destination
or --postdest are provided.

=item C<--pmml>

Requests conversion of math to Presentation MathML.
Conversion is the default for xhtml format.

=item C<--cmml>

Requests or disables conversion of math to Content MathML.
Conversion is disabled by default.
B<Note> that this conversion is only partially implemented.

=item C<--openmath>

Requests or disables conversion of math to OpenMath.
Conversion is disabled by default.
B<Note> that this conversion is not yet supported in C<latexmls>.

=item C<--keepTeX>

By default, when any of the MathML or OpenMath conversions
are used, the source TeX formula will be removed;
This option preserves it as a MathML annotation in parallel markup.
B<Note> that C<--parallelmath> and C<--pmml> or C<--cmml> are required.

=item C<--keepXMath>

By default, when any of the MathML or OpenMath conversions
are used, the intermediate math representation will be removed;
This option preserves it as a MathML annotation in parallel markup.
B<Note> that C<--parallelmath> and C<--pmml> or C<--cmml> are required.

=item C<--parallelmath>

Requests or disables parallel math markup.
Parallel markup is the default for xhtml formats when multiple math
formats are requested.

This method uses the MathML C<semantics> element with additional formats
appearing as C<annotation>'s.
The first math format requested must be either Presentation or Content MathML;
additional formats may be MathML or OpenMath.

If this option is disabled and multiple formats are requested, the
representations are simply stored as separate children of the C<Math> element.


=item C<--stylesheet>=I<file>

Sets a stylesheet of choice to be used by the postprocessor. Requires C<--post>

=item C<--css>=I<cssfile>

Adds I<cssfile> as a css stylesheet to be used in the transformed html/xhtml.
Multiple stylesheets can be used; they are included in the html in the
order given, following the default C<core.css>
(but see C<--nodefaultcss>). Some stylesheets included in the distribution are
  --css=navbar-left   Puts a navigation bar on the left.
                      (default omits navbar)
  --css=navbar-right  Puts a navigation bar on the left.
  --css=theme-blue    A blue coloring theme for headings.
  --css=amsart        A style suitable for journal articles.

=item C<--nodefaultcss>

Disables the inclusion of the default C<core.css> stylesheet.

=item C<--nocomments>

Normally latexml preserves comments from the source file, and adds a comment every 25 lines as
an aid in tracking the source.  The option --nocomments discards such comments.

=item C<--inputencoding=>I<encoding>

Specify the input encoding, eg. C<--inputencoding=iso-8859-1>.
The encoding must be one known to Perl's Encode package.
Note that this only enables the translation of the input bytes to
UTF-8 used internally by LaTeXML, but does not affect catcodes.
In such cases, you should be using the inputenc package.
Note also that this does not affect the output encoding, which is
always UTF-8.

=item C<--VERSION>

Shows the version number of the LaTeXML package..

=item C<--debug>=I<package>

Enables debugging output for the named package. The package is given without the leading LaTeXML::.

=item C<--help>

Shows this help message.

=back

=head1 SEE ALSO

L<latexmlc>, L<latexmld>, L<latexml>, L<latexmlpost>, L<latexmlmath>, L<LaTeXML>

=cut
#**********************************************************************

